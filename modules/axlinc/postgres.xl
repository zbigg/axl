#!/usr/bin/xparse
#
# File:		postgres.xl
#
# Id:		$Id: postgres.xl 1.2 03/11/27 22:34:06+01:00 zbigg@zbigg.(none) $
#
# Copyright:
#
#   This program is Copyright(c) Zbigniew Zagorski 2001.
#   All rights reserved, and is distributed as free software under the 
#   license in the file "LICENSE", which is included in the distribution.
if( ! _postgres_included ){
    _postgres_included = 1;
    _postgres_creator = builtin "postgres:create_postgres_object" safe;
    if( ! _postgres_creator ) {
	postgres = new;
	postgres.connect = func(a,b,c,d,e,f) -> ( 0 );
	postgres.error = "PostgreSQL module can't be loaded";
    } else {
	postgres = _postgres_creator();
    };
    
    /* 
	postgres.connect( dbname, host, port, login, pwd ) : connection
	postgres.error : string
	
	connection.exec(query) : result
	connection.error : string
	connection.close()
	
	result.close()
	    
	result.ntuples : integer
	    Obtain number of tuples in thre result.
	    
	result.nfields : integer
	    Obtain number of fields in thre tuple.
	    
	result.getvalue( tuple, field ) : string
	    Obtain value of pointed by tuble and record numbers.
	    
	result.fname( i ) : string		
	    Obtain name of i-th field.
	    
	result.begin() : seek result to start.

	result.get() : get object with fields as in each record
	resulr.empty() : test if pointer is at end of result
    */
    postgres.name = "PosgreSQL";
    with ( postgres ) {
	Bconnect = connect;
	connect = func( dbname, host, port, login, pwd ) {
	    conn = Bconnect( dbname,host,port,login,pwd );
	    conn.name = "PostgreSQL " + login + " at " + dbname +"@" + host; 
	    if( !conn )
		return 0;
	    conn.Bexec = conn.exec;
	    conn.exec = func(query) {
		result = Bexec(query);
		if( !result ) return 0;
		result.begin = func() { 
		    this._row_number = 0; 
		    this.robj = 0;
		};
		result.get = func() {
		    if( _row_number < ntuples ) {
			if( !robj )
			    robj = new;
			for( i = 0; i < nfields; i+=1 )
			    robj[ fname(i) ] = getvalue( _row_number,i );
			_row_number+=1;
			return robj;
		    } else return 0;
		};
		result.empty = func() { return _row_number >= ntuples; };
		return result;
	    }; // connection.exec
	    return conn;
	}; // postgres.connect
    }; // with postgres
};

